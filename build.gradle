plugins {
	id 'org.springframework.boot' version '2.6.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'application'
	id 'org.liquibase.gradle' version '2.1.0'
	id 'com.github.johnrengelman.processes' version '0.5.0'
	id 'org.springdoc.openapi-gradle-plugin' version '1.3.3'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

application {
	mainClass = 'com.example.fabric.FabricApplication'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compileOnly 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	implementation(
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.springframework.boot:spring-boot-starter-web',
			'org.springframework.boot:spring-boot-starter-actuator',
			'org.springframework.boot:spring-boot-starter-security',
			'io.jsonwebtoken:jjwt-api:0.11.2',
			'org.springdoc:springdoc-openapi-ui:1.5.12',
			'org.liquibase:liquibase-core:4.6.2',
	)

	annotationProcessor(
			'org.projectlombok:lombok:1.18.22',
			'javax.annotation:javax.annotation-api:1.3.2',
			'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final'
	)

	liquibaseRuntime(
			sourceSets.main.output,
			'org.liquibase:liquibase-core:4.6.2',
			'org.liquibase.ext:liquibase-hibernate5:4.6.2',
			'org.springframework.boot:spring-boot-starter-data-jpa',
	)

	runtimeOnly(
			'com.h2database:h2:1.4.200',
			'io.jsonwebtoken:jjwt-impl:0.11.2',
			'io.jsonwebtoken:jjwt-jackson:0.11.2',
	)
}

diffChangeLog {
	dependsOn compileJava
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog/db.changelog-master.xml'
			url 'jdbc:h2:./socialPoll'
			username 'test'
			password 'test'
			referenceUrl 'hibernate:spring:com.example.fabric.model.' +
					'?dialect=org.hibernate.dialect.H2Dialect' +
					'&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
		}
	}
}

openApi {
	apiDocsUrl.set("http://localhost:5000/v3/api-docs")
}

tasks.named('test') {
	useJUnitPlatform()
}
